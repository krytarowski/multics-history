"	BEGIN INCLUDE FILE probe_request_macros.incl.alm
"	Created (from subsys_request_macros) 04/08/79 W. Olin Sibert
"
" Macros to create request table(s) for probe and probe extensions


	equ	request_table_version,1

	bool	flags.is_request,040000	" this entry is a request
	bool	flags.is_builtin,020000	" this entry is a builtin function

	bool	flags.unimplemented,400000	" this request is not yet implemented
	bool	flags.secret,200000 	" this is an internal request, and doesn't show up in a "?" listing


"
"	begin_table tablename
"

	macro	begin_table
	segdef	&1
	maclist	object,save
	use	tables
&1:	vfd	36/request_table_version
	aci	@&1@,32
	dup	6		" six words of padding, for later use as flags
	vfd	36/0
	dupend
	vfd	36/&1.rc
	maclist	off
	set	.rc,0
	maclist	restore
	&end

"
"	Request generating macro. Intended use:
"
"	request	name,(alias),segname$entryname,
"		(Short description of request.){,flags}
"

	macro	request
	maclist	off,save
	set	.rc,.rc+1
	maclist	object
	use	tables
	vfd	18/&U.call,18/&U.names
&=&4,&[	vfd	36/0
&;	vfd	18/&U.info,18/&l4
	vfd	36/0		" usage string info will go here
	maclist	off
	use	info
&U.info:	aci	@&4@
&]
	use	tables
&=&5,&[	set	.flags,flags.is_request
&;	set	.flags,&5+flags.is_request
&]
	maclist	object
	vfd	18/.flags,18/0
	maclist	off
	use	calls
&U.call:	epaq	*
	lprp4	7|stack_header.lot_ptr,*au
	call6	&3
	use	names
&U.names: namelist	&1&^=&2,&[,&2&]
	maclist	restore
	&end

"
"	Macro for generating builtins. Syntax is same as request macro
"

	macro	builtin
	maclist	off,save
	set	.rc,.rc+1
	maclist	object
	use	tables
	vfd	18/&U.call,18/&U.names
&=&4,&[	vfd	36/0
&;	vfd	18/&U.info,18/&l4
	vfd	36/0		" usage string info will go here
	maclist	off
	use	info
&U.info:	aci	@&4@
&]
	use	tables
&=&5,&[	set	.flags,flags.is_builtin
&;	set	.flags,&5+flags.is_builtin
&]
	maclist	object
	vfd	18/.flags,18/0
	maclist	off
	use	calls
&U.call:	epaq	*
	lprp4	7|stack_header.lot_ptr,*au
	call6	&3
	use	names
&U.names: namelist	&1&^=&2,&[,&2&]
	maclist	restore
	&end

"
"	internal macro for generating name lists
"

	macro	namelist
	maclist	off,save
	vfd	36/&K
&R&(	aci	@&i@,32
&)
	maclist	restore
	&end

"
"	end_table tablename
"

	macro	end_table
	maclist	off,save
	set	&1.rc,.rc
	maclist	restore
	&end


	use	tables
	use	calls
	use	names
	use	info
	join	/text/tables,calls,names,info

	equ	stack_header.lot_ptr,22

"	END INCLUDE FILE probe_request_macros.incl.alm
"
"
"                                          -----------------------------------------------------------
"
"
"
" Historical Background
"
" This edition of the Multics software materials and documentation is provided and donated
" to Massachusetts Institute of Technology by Group Bull including Bull HN Information Systems Inc. 
" as a contribution to computer science knowledge.  
" This donation is made also to give evidence of the common contributions of Massachusetts Institute of Technology,
" Bell Laboratories, General Electric, Honeywell Information Systems Inc., Honeywell Bull Inc., Groupe Bull
" and Bull HN Information Systems Inc. to the development of this operating system. 
" Multics development was initiated by Massachusetts Institute of Technology Project MAC (1963-1970),
" renamed the MIT Laboratory for Computer Science and Artificial Intelligence in the mid 1970s, under the leadership
" of Professor Fernando Jose Corbato. Users consider that Multics provided the best software architecture for
" managing computer hardware properly and for executing programs. Many subsequent operating systems
" incorporated Multics principles.
" Multics was distributed in 1975 to 2000 by Group Bull in Europe , and in the U.S. by Bull HN Information Systems Inc., 
" as successor in interest by change in name only to Honeywell Bull Inc. and Honeywell Information Systems Inc. .
"
"                                          -----------------------------------------------------------
"
" Permission to use, copy, modify, and distribute these programs and their documentation for any purpose and without
" fee is hereby granted,provided that the below copyright notice and historical background appear in all copies
" and that both the copyright notice and historical background and this permission notice appear in supporting
" documentation, and that the names of MIT, HIS, Bull or Bull HN not be used in advertising or publicity pertaining
" to distribution of the programs without specific prior written permission.
"    Copyright 1972 by Massachusetts Institute of Technology and Honeywell Information Systems Inc.
"    Copyright 2006 by Bull HN Information Systems Inc.
"    Copyright 2006 by Bull SAS
"    All Rights Reserved
"
"