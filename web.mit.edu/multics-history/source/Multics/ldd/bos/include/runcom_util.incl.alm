" 
" Begin include file ...... runcom_util.incl.alm
" Created 9/17/75 by Noel I. Morris


	equ	mbufl,1204	length of runcom buffer


"	lda	name
"	tsx2	runcom_setup	setup to read from runcom file
"	tra	error		file not found

runcom_setup:
	stx2	sx2		setup to read from a macro file
	sta	rctemp		save name in case of error

	tsx2	search
	tra	rsnf		macro file not found

	cana	=o4000,dl		check for macro bit
	tze	rsnf		if not macro

	ana	=o777777770000
	sta	com|macroloc

	ora	=1,dl		prime buffer
	tsx2	rdsec
	nop	com|macrobuf
	tra	rserr		if error

	lda	com|inputsw	stack inputsw
	arl	6
	ora	=o020000,du	and set to macro
	sta	com|inputsw

sx2:	eax2	*		restore X2
	tra	1,2		take normal return


rsnf:	tsx2	erpt		macro file not found
	acc	"file ^g not found"
	arg	rctemp

rserr:	ldx2	sx2		restore X2
	tra	0,2		take error return

" 

"	tsx2	make_runcom_file	to format input lines into file
"	tra	get_input_line	subr to read into "line"
"	tra	error		error if file too big
"	stq	length,dl		length of file in words

make_runcom_file:
	stx2	mkrx2		save XR's
	stx0	mkrx0		..

	eax0	0		X0 is buffer index
mkrloop:	tsx2	mkrx2,*		call input subroutine
	tra	mark		tra if end of input

	tsx2	runcom_line_len	get length of line and set line mark
	stq	rctemp		save length

	eax7	0,0		current offset in X7
	adlx7	rctemp		add length of this line
	cmpx7	mbufl*4,du	check for buffer overflow
	tpl	runcom_too_big	..

	mlr	(rl),(pr,rl,x0)	copy line into macro buffer
	desc9a	pagectl,qu
	desc9a	bf|0,qu

	eax0	0,7		get offset of next line in X0
	tra	mkrloop		and loop


mark:	eaq	4,0		get offset plus 1 word in QU
	qrl	18+2		shift to QL and compute word count

mkrx2:	eax2	*		restore XR's
mkrx0:	eax0	*		..
	tra	2,2		take normal return


runcom_too_big:
	tsx2	erpt		complain if runcom too large
	acc	"file too big."
	ldx2	mkrx2		restore XR's
	ldx0	mkrx0		..
	tra	1,2		and take error return

" 

"	lda	name		name of runcom file
"	ldq	length,dl		length of runcom file in words
"	tsx2	store_runcom_file	to store completed runcom
"	tra	error		file system error

store_runcom_file:
	stx2	srfx2		save X2
	sta	rctemp		save name

	lca	1,dl		make fence for end
	sta	bf|-1,ql		and cap end of macro

	adq	63,dl		round to nearest disk sector
	qrl	6		compute number of sectors

	lda	rctemp		get name of runcom
	tsx2	makefil		create a file for runcom
	tra	srferr

	ldq	=o4000,dl		get runcom ID bit
	orsq	com|1,7		insert in BOS dir entry

	tsx2	wtsec		write out the runcom
	nop	bf|0
	tra	srferr

srfx2:	eax2	*		restore X2
	tra	1,2		that's all


srferr:	ldx2	srfx2		restore X2
	tra	0,2		take error return

"

"	tsx2	runcom_line_len	to get line length
"	stq	len,du		length of line in words

runcom_line_len:
	stx2	rllx2		save X2

	tsx2	stripg		strip trailing blanks from line
	desc6a	line,84		..
	lrl	36		length in Q
	adq	5,dl		round up to nearest word
	div	6,dl		compute word count
	qls	18		in QU
	stcq	pagectl,70	insert in line mark

	adq	1,du		include line mark in length
	qls	2		get character count

rllx2:	eax2	*		restore X2
	tra	0,2		return to caller


rctemp:	bss	,1


" End of include file ...... runcom_util.incl.alm

"
"
"                                          -----------------------------------------------------------
"
"
"
" Historical Background
"
" This edition of the Multics software materials and documentation is provided and donated
" to Massachusetts Institute of Technology by Group Bull including Bull HN Information Systems Inc. 
" as a contribution to computer science knowledge.  
" This donation is made also to give evidence of the common contributions of Massachusetts Institute of Technology,
" Bell Laboratories, General Electric, Honeywell Information Systems Inc., Honeywell Bull Inc., Groupe Bull
" and Bull HN Information Systems Inc. to the development of this operating system. 
" Multics development was initiated by Massachusetts Institute of Technology Project MAC (1963-1970),
" renamed the MIT Laboratory for Computer Science and Artificial Intelligence in the mid 1970s, under the leadership
" of Professor Fernando Jose Corbato. Users consider that Multics provided the best software architecture for
" managing computer hardware properly and for executing programs. Many subsequent operating systems
" incorporated Multics principles.
" Multics was distributed in 1975 to 2000 by Group Bull in Europe , and in the U.S. by Bull HN Information Systems Inc., 
" as successor in interest by change in name only to Honeywell Bull Inc. and Honeywell Information Systems Inc. .
"
"                                          -----------------------------------------------------------
"
" Permission to use, copy, modify, and distribute these programs and their documentation for any purpose and without
" fee is hereby granted,provided that the below copyright notice and historical background appear in all copies
" and that both the copyright notice and historical background and this permission notice appear in supporting
" documentation, and that the names of MIT, HIS, Bull or Bull HN not be used in advertising or publicity pertaining
" to distribution of the programs without specific prior written permission.
"    Copyright 1972 by Massachusetts Institute of Technology and Honeywell Information Systems Inc.
"    Copyright 2006 by Bull HN Information Systems Inc.
"    Copyright 2006 by Bull SAS
"    All Rights Reserved
"
"