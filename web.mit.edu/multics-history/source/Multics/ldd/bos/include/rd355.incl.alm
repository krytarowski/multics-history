" 
" Begin include file ...... rd355.incl.alm
" Modified April, 1981, by Charlie Hornig, to move the 355 mailboxes


	bool	dn355mbx,3400	location of DataNet 355 mailbox
	bool	bit22,040000	bit 22 - parity bit for PCW
	bool	dump,000075	dump op-code for DIA

" 
" READ_355_WORDS - Read a number of words from DataNet 355.
"
"	tsx2	read_355_words
"	arg	indword		must be followed by data buffer
"	zero	355_address,no_36bit_words

read_355_words:
	stx2	r3exit		save return point

	absa	0,2*		get location of indword
	als	6		in AU
	ora	dump,dl		OR in dump op-code
	tsx2	parity		get parity on PCW
	ldq	mbx_offset	get offset of this mbx
	sta	mem|dn355mbx,ql	put PCW in 355 mailbox

	ldx2	r3exit		restore X2
	lda	1,2		get 355 location and count
	ora	=o100000,du	set 36-bit transfer mode
	sta	0,2*		store in indword

	ldq	iom_chan		get channel number
	lda	4*3,dl		get interrupt cell number
	cmpq	32,dl		check the channel number
	tmi	*+2		all ok, continue
	sba	4,dl		fix the interrupt cell number
	ada	iom_no		add in the iom number
	eaa	com|iom_mxbase,*al	add in mbx base addr
	sta	norm_int_cell	save it away

	anq	31,dl		get channel number mod 32
	lda	=o400000,du	get a bit
	arl	0,ql		move the bit
	sta	intbit		save the bit

	ldx7	norm_int_cell	point to cells
	stz	mem|0,7		clear all pending interrupts
	stz	mem|16,7		also for emergency

	tsx2	connect_iom
	zero	diapcw,0		connect to dia via iom

	ldx7	norm_int_cell	get addr of int cell
wait_for_int:
	lda	intbit		look for interrupt
	cana	mem|0,7		is this it?
	tnz	r3exit		yes, done
	cana	mem|16,7		no, was it emergency-level?
	tze	wait_for_int	nope, loop for more

r3exit:	eax2	*		restore return addr
	tra	2,2		return

intbit:	oct	0
norm_int_cell:
	oct	0
" 
"	Subroutine to calculate parity on ica pcw. Enter with A reg
"	holding word on which parity is to be calculated. Returns the word
"	in the A-reg with parity bit set.

parity:	null
	sta	atemp		save input word
	gtb	0		calculate parity
	ana	1,dl		test for odd or even
	tnz	3,ic		odd
	lda	bit22,dl		even..set parity bit
	orsa	atemp		..
	lda	atemp		get pcw again
	tra	0,2		return

	bss	atemp,1		temp storage

" End of include file ...... rd355.incl.alm

"
"
"                                          -----------------------------------------------------------
"
"
"
" Historical Background
"
" This edition of the Multics software materials and documentation is provided and donated
" to Massachusetts Institute of Technology by Group Bull including Bull HN Information Systems Inc. 
" as a contribution to computer science knowledge.  
" This donation is made also to give evidence of the common contributions of Massachusetts Institute of Technology,
" Bell Laboratories, General Electric, Honeywell Information Systems Inc., Honeywell Bull Inc., Groupe Bull
" and Bull HN Information Systems Inc. to the development of this operating system. 
" Multics development was initiated by Massachusetts Institute of Technology Project MAC (1963-1970),
" renamed the MIT Laboratory for Computer Science and Artificial Intelligence in the mid 1970s, under the leadership
" of Professor Fernando Jose Corbato. Users consider that Multics provided the best software architecture for
" managing computer hardware properly and for executing programs. Many subsequent operating systems
" incorporated Multics principles.
" Multics was distributed in 1975 to 2000 by Group Bull in Europe , and in the U.S. by Bull HN Information Systems Inc., 
" as successor in interest by change in name only to Honeywell Bull Inc. and Honeywell Information Systems Inc. .
"
"                                          -----------------------------------------------------------
"
" Permission to use, copy, modify, and distribute these programs and their documentation for any purpose and without
" fee is hereby granted,provided that the below copyright notice and historical background appear in all copies
" and that both the copyright notice and historical background and this permission notice appear in supporting
" documentation, and that the names of MIT, HIS, Bull or Bull HN not be used in advertising or publicity pertaining
" to distribution of the programs without specific prior written permission.
"    Copyright 1972 by Massachusetts Institute of Technology and Honeywell Information Systems Inc.
"    Copyright 2006 by Bull HN Information Systems Inc.
"    Copyright 2006 by Bull SAS
"    All Rights Reserved
"
"