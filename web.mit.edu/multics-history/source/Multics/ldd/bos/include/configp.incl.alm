" 
" Begin include file ...... configp.incl.alm
" Created 9/15/75 by Noel I. Morris

"	eax0	offset		" X0 -> config card
"	lda	com|conf,0	" Card name in A.
"	tsx2	config_print	" Convert config card for printing.

config_print:
	stx2	pcd_x2		" Save XR's.
	mlr	(),(),fill(20)	" Blank output line.
	desc6a	*,0
	desc6a	line,84

	eax7	0,0		" Get address of config card.
	eax6	0		" Current position in output buffer.
	ldq	com|conf+confl-1,7	" Two bits per field indicating original type.
	stq	pcd_types
	eax5	0,ql		" Parameter count in X5.
	anx5	=o77,du		" Mask count.
	tra	pcd_ascii		" Convert card name.

pcd_next:
	lda	com|conf,7	" Next raw item.
	ldq	pcd_types
	qlr	2
	stq	pcd_types
	anq	3,dl		" Type code for this item.

	tra	*+1,ql		" Dispatch on it.
	tra	pcd_oct		"  Octal number.
	tra	pcd_tag		"  Tag (A - H).
	tra	pcd_ascii		"  ASCII field (four characters).
	tra	pcd_dec		"  Decimal number.

pcd_oct:	tsx2	octwd		" Convert to BCD.
	staq	pcd_temp1		" Store converted octal number.

	mve	(),(),()		" Move and edit to suppress leading zeroes.
	desc6a	pcd_temp1,12
	desc9a	coedit,4
	desc6a	pcd_temp2,12

	lda	12,dl		" 12 characters to insert in output.
	tra	pcd_join

pcd_dec:	sta	pcd_temp0		" Store binary number.

	btd	(),()		" Convert to decimal.
	desc9a	pcd_temp0,4
	desc4ls	pcd_temp1,12

	mvne	(),(),()		" Suppress leading zeroes and insert decimal point.
	desc4ls	pcd_temp1,12
	desc9a	cdedit,12
	desc6a	pcd_temp2,13

	lda	13,dl		" 13 characters to insert in output.
	tra	pcd_join

pcd_tag:	cmpa	8,dl		" Must be less than 8.
	trc	pcd_oct		" Otherwise, convert as octal.

	ada	=h      "		" Make into letter.
	tra	pcd_bcd		" And treat as BCD string.

pcd_ascii:
	tsx2	asge		" Convert to BCD.
	tsx2	rjust		" Right-justify 4 characters.

pcd_bcd:	sta	pcd_temp2	" Store BCD.

	lda	6,dl		" 6 characters to insert in output.
	tra	pcd_join

pcd_join:
	scmr	(rl),(du)		" Search backward for first blank.
	desc6a	pcd_temp2,al
	bci	" "
	arg	pcd_temp0

	ldq	pcd_temp0		" Get tally.
	sba	pcd_temp0		" Compute non-blank count.

	mlr	(rl,al),(rl,x6)	" Copy characters into output line.
	desc6a	pcd_temp2,ql
	desc6a	line,ql

	qls	18		" Character count in QU.
	stq	pcd_temp0		" Store it.
	adx6	pcd_temp0		" Add to offset.
	eax6	1,6		" Plus one for white space.

	eax7	1,7		" Step to next parameter.
	eax5	-1,5		" Count one parameter.
	tpl	pcd_next		" Go on to next parameter.

pcd_x2:	eax2	*
	tra	0,2

pcd_types:
	bss	,1		" Type codes.
pcd_temp0:
	bss	,1
	even
pcd_temp1:
	bss	,2
pcd_temp2:
	bss	,3

coedit:	vfd	9/lte+1,o9/20,9/mvzb+11,9/mvc+1

cdedit:	vfd	9/lte+1,o9/20,9/lte+3,o9/20,9/lte+4,o9/52,9/mfls+10,9/enf
	vfd	9/mvc+1,9/lte+1,o9/33,9/insm+1

" End of include file ...... configp.incl.alm

"
"
"                                          -----------------------------------------------------------
"
"
"
" Historical Background
"
" This edition of the Multics software materials and documentation is provided and donated
" to Massachusetts Institute of Technology by Group Bull including Bull HN Information Systems Inc. 
" as a contribution to computer science knowledge.  
" This donation is made also to give evidence of the common contributions of Massachusetts Institute of Technology,
" Bell Laboratories, General Electric, Honeywell Information Systems Inc., Honeywell Bull Inc., Groupe Bull
" and Bull HN Information Systems Inc. to the development of this operating system. 
" Multics development was initiated by Massachusetts Institute of Technology Project MAC (1963-1970),
" renamed the MIT Laboratory for Computer Science and Artificial Intelligence in the mid 1970s, under the leadership
" of Professor Fernando Jose Corbato. Users consider that Multics provided the best software architecture for
" managing computer hardware properly and for executing programs. Many subsequent operating systems
" incorporated Multics principles.
" Multics was distributed in 1975 to 2000 by Group Bull in Europe , and in the U.S. by Bull HN Information Systems Inc., 
" as successor in interest by change in name only to Honeywell Bull Inc. and Honeywell Information Systems Inc. .
"
"                                          -----------------------------------------------------------
"
" Permission to use, copy, modify, and distribute these programs and their documentation for any purpose and without
" fee is hereby granted,provided that the below copyright notice and historical background appear in all copies
" and that both the copyright notice and historical background and this permission notice appear in supporting
" documentation, and that the names of MIT, HIS, Bull or Bull HN not be used in advertising or publicity pertaining
" to distribution of the programs without specific prior written permission.
"    Copyright 1972 by Massachusetts Institute of Technology and Honeywell Information Systems Inc.
"    Copyright 2006 by Bull HN Information Systems Inc.
"    Copyright 2006 by Bull SAS
"    All Rights Reserved
"
"