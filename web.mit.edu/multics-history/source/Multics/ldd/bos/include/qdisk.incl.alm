" 
" Begin include file ..... qdisk.incl.alm
"	Created 6/1/76 by Noel I. Morris
"	Modified 10/31/77 by Greenberg for bulk check, time print.

" To test if disk is a Multics storage system volume:
"
"	tsx2	query_disk
"	arg	devt
"	tra	error
"	tra	not_vol


query_disk:
	stx2	qdkx2		save X2

	ldq	0,2*		get devt
	stq	qdkdevt		and save
	eaa	0,ql		test for bulk
	ana	=o17,du
	cmpa	=1,du
	tze	qdknv		don't hassle it, it's bulk

	eax7	0,ql		config deck index in X7
	anx7	=o777760,du	mask it
	lda	com|DISK_cd.sub,7	get subsystem name
	sta	qdksubsys		and save

	qrl	18		save drive number
	anq	=o777,dl		..
	stq	qdkno		..


	lda	0,dl		label is record zero
	tsx2	mulbos		convert to BOS address
	arg	qdkdevt		..
	tra	qdkerr

	ana	=o777777770000	mask sector number
	sta	qdkadr		save for next I/O
	adla	=v24/label.Multics/64,12/1  get sector with label

	tsx2	rdev		read the label
	arg	qdkdevt
	nop	qdkbuf
	tra	qdkerr

	cmpc	(),()		see if Multics volume
	desc9a	qdkbuf+label.Multics-label.Multics/64*64,32
	desc9a	Multics_label,32
	tnz	qdknv		if not, take not_vol return

	tsx2	erpt		print message
	acc	"^a ^d is Multics storage system volume ^A."
	arg	qdksubsys
	arg	qdkno
	arg	qdkdesc

	lda	qdkadr		get last-use time
	adla	=v24/label.time_map_updated/64,12/1

	tsx2	rdev
	arg	qdkdevt
	nop	qdkbuf
	tra	qdkerr

	ldaq	qdkbuf+label.time_map_updated-label.time_map_updated/64*64
	tsx2	cvclock
	staq	qdktime		convert the time

	tsx2	erpt
	acc	"Label last written at ^g ^g."
	arg	qdktime+0
	arg	qdktime+1


qdkx2:	eax2	*		restore X2
	tra	3,2		take is volume return


qdknv:	ldx2	qdkx2		restore X2
	tra	2,2		disk is not storage system volume


qdkerr:	ldx2	qdkx2		restore X2
	tra	1,2		take error return


" 

"	To ask if disk may be overwritten:
"
"	tsx2	ask_disk
"	tra	no
"

ask_disk:	stx2	adkx2		save X2

	tsx2	erpt		pop the question
	acc	"Do you wish to write on ^a ^d^g"
	arg	qdksubsys
	arg	qdkno
	arg	=h!!????

	tsx2	readtty		read the answer
	tra	*-1		..

	tsx2	scan		scan input

adkx2:	eax2	*		restore X2
	lda	arg		get answer
	cmpa	=h   yes		yes?
	tze	1,2
	tra	0,2


" 

qdkdevt:	bss	,1
qdksubsys:bss	,1
qdkno:	bss	,1
qdkadr:	bss	,1
	even
qdktime:	bss	,2

qdkdesc:	desc9a	qdkbuf+label.pv_name-label.pv_name/64*64,32

Multics_label:
	aci	"Multics Storage System Volume",32

	even
qdkbuf:	bss	,64


" End of include file ...... qdisk.incl.alm

"
"
"                                          -----------------------------------------------------------
"
"
"
" Historical Background
"
" This edition of the Multics software materials and documentation is provided and donated
" to Massachusetts Institute of Technology by Group Bull including Bull HN Information Systems Inc. 
" as a contribution to computer science knowledge.  
" This donation is made also to give evidence of the common contributions of Massachusetts Institute of Technology,
" Bell Laboratories, General Electric, Honeywell Information Systems Inc., Honeywell Bull Inc., Groupe Bull
" and Bull HN Information Systems Inc. to the development of this operating system. 
" Multics development was initiated by Massachusetts Institute of Technology Project MAC (1963-1970),
" renamed the MIT Laboratory for Computer Science and Artificial Intelligence in the mid 1970s, under the leadership
" of Professor Fernando Jose Corbato. Users consider that Multics provided the best software architecture for
" managing computer hardware properly and for executing programs. Many subsequent operating systems
" incorporated Multics principles.
" Multics was distributed in 1975 to 2000 by Group Bull in Europe , and in the U.S. by Bull HN Information Systems Inc., 
" as successor in interest by change in name only to Honeywell Bull Inc. and Honeywell Information Systems Inc. .
"
"                                          -----------------------------------------------------------
"
" Permission to use, copy, modify, and distribute these programs and their documentation for any purpose and without
" fee is hereby granted,provided that the below copyright notice and historical background appear in all copies
" and that both the copyright notice and historical background and this permission notice appear in supporting
" documentation, and that the names of MIT, HIS, Bull or Bull HN not be used in advertising or publicity pertaining
" to distribution of the programs without specific prior written permission.
"    Copyright 1972 by Massachusetts Institute of Technology and Honeywell Information Systems Inc.
"    Copyright 2006 by Bull HN Information Systems Inc.
"    Copyright 2006 by Bull SAS
"    All Rights Reserved
"
"