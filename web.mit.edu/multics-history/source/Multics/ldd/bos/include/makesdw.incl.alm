" 
" Begin include file ...... makesdw.incl.alm
"	Created 9/9/76 by Noel I. Morris
"	Modified 8/30/78 by Greenberg for checking flagbox and asking.

"	lda	length,du		segment length in words
"	tsx2	makesdw		allocate segment
"	 tra	quit		exit if core clobber avoided
"	staq	ds|2*segno	returned SDW
"	cams	0		don't forget this
"

makesdw:
	eaq	63,au		length in QU rounded up
	anq	=o777700,du	make 0 mod 64
	stq	mksdlen		save length
"
"	Check to see if core will be clobbered.
"
	lda	mksd.rtb_ptr,*	check flag for ssenb
	cana	fgbx.ssenb,du	Is it enabled?
	tze	mksd.ssenb_ok
"
"	Query the operator about destroying core.
"
	stx2	mksd.savex2
	mlr	(),()
	desc6a	line,84		save "line"
	desc6a	mksd.saveline,84
mksd.query:
	tsx2	erpt		complain
	acc	"Multics has not been shut down.  This command will destroy"
	tsx2	erpt
	acc	"the contents of main memory. Do you wish to proceed^g"
	arg	=h!!????

	tsx2	readtty
	tra	mksd.payon	Please answer yes or no.

	lda	line		Investigate answer.
	cmpa	=hyes   "
	tze	mksd.yes_clobber
	cmpa	=hno    "
	tnz	mksd.payon
	ldx2	mksd.savex2	Don't bother restoring line.
	tra	0,2		Error exit.

mksd.payon:
	tsx2	erpt
	acc	"Please answer yes or no."
	tra	mksd.query

mksd.yes_clobber:
	lcx7	fgbx.ssenb+1,du	Turn off ssenb.
	ansx7	mksd.rtb_ptr,*

	mlr	(),()
	desc6a	mksd.saveline,84
	desc6a	line,84
mksd.savex2:
	eax2	*-*
mksd.ssenb_ok:
	eax2	1,2		Set good exit
	lda	ds|pgm*2+1	get SDW word B for main procedure
	ana	sdw.bound,du	mask the bound field
	adla	=1b32,du		convert bound to length
	arl	3-4+6		shift to SDW address position
	adla	=63b23,dl		round up to nearest 64-word block
	ana	=o777777,du	..
	adla	ds|pgm*2		add to SDW word A
	stac	mksdsdw		save it if not saved already
	lda	mksdsdw		now have SDW word A in A

	qrl	6		place length in SDW address position
	adlq	mksdsdw		add length to address
	stq	mksdsdw		save new address

	ldq	mksdlen		length in QU
	qrl	-3+4		shift to bound position and divide by 16
	sblq	=1b32,du		adjust for bound specification
	erq	ds|pgm*2+1	insert other SDW word B bits
	anq	sdw.bound,du	mask bound field
	erq	ds|pgm*2+1	now have SDW word B in Q

	tra	0,2		return to caller

mksdsdw:	bss	,1
mksdlen:	bss	,1
mksd.saveline:
	bss	,14		Save for "line"
	even
mksd.rtb_ptr:
	its	fgb,fgbx.rtb

" End of include file ...... makesdw.incl.alm


"
"
"                                          -----------------------------------------------------------
"
"
"
" Historical Background
"
" This edition of the Multics software materials and documentation is provided and donated
" to Massachusetts Institute of Technology by Group Bull including Bull HN Information Systems Inc. 
" as a contribution to computer science knowledge.  
" This donation is made also to give evidence of the common contributions of Massachusetts Institute of Technology,
" Bell Laboratories, General Electric, Honeywell Information Systems Inc., Honeywell Bull Inc., Groupe Bull
" and Bull HN Information Systems Inc. to the development of this operating system. 
" Multics development was initiated by Massachusetts Institute of Technology Project MAC (1963-1970),
" renamed the MIT Laboratory for Computer Science and Artificial Intelligence in the mid 1970s, under the leadership
" of Professor Fernando Jose Corbato. Users consider that Multics provided the best software architecture for
" managing computer hardware properly and for executing programs. Many subsequent operating systems
" incorporated Multics principles.
" Multics was distributed in 1975 to 2000 by Group Bull in Europe , and in the U.S. by Bull HN Information Systems Inc., 
" as successor in interest by change in name only to Honeywell Bull Inc. and Honeywell Information Systems Inc. .
"
"                                          -----------------------------------------------------------
"
" Permission to use, copy, modify, and distribute these programs and their documentation for any purpose and without
" fee is hereby granted,provided that the below copyright notice and historical background appear in all copies
" and that both the copyright notice and historical background and this permission notice appear in supporting
" documentation, and that the names of MIT, HIS, Bull or Bull HN not be used in advertising or publicity pertaining
" to distribution of the programs without specific prior written permission.
"    Copyright 1972 by Massachusetts Institute of Technology and Honeywell Information Systems Inc.
"    Copyright 2006 by Bull HN Information Systems Inc.
"    Copyright 2006 by Bull SAS
"    All Rights Reserved
"
"