" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1982 *
" *                                                         *
" ***********************************************************
"tape dump prog
" Modified on 11/9/71 by Noel I. Morris

"  ******************************************************
"  *                                                    *
"  *                                                    *
"  * Copyright (c) 1972 by Massachusetts Institute of   *
"  * Technology and Honeywell Information Systems, Inc. *
"  *                                                    *
"  *                                                    *
"  ******************************************************


	include	bosequ


	equ	bufl,1040		max record size

	stx2	x2
	ldx7	mem|0,2
	lda	mem|arglen+1+1,7	get tape number in decimal
	tmoz	*+5
	cmpa	=16,du
	tpnz	*+3
	als	12
	stca	tpch,04		store as device number

	tsx2	init_io
	tsx2	ttyinit
	arg	ttyrequest

	tsx2	itaper		initialize tape for reading
	arg	tpch

	lda	=h ptpkg
	eaq	pt_org		load the print package
	tsx2	ldcom
	tra	x2

	eax7	nheading_lth-1	copy the page heading
	lda	nheading,7
	sta	pthdr,7
	eax7	-1,7
	tpl	*-3

	tsx2	rdclock		put date and time in header
	sta	pthdr+3
	stq	pthdr+4

	ldx2	x2		restore X2
	lxl7	mem|0,2		get address of command line
	eax6	pthdr+6		get address of header line
odd;	rpd	14,1		copy command line in header
	lda	mem|0,7		..
	sta	0,6		..

	tsx2	printon		turn printer on

	tsx2	headform

loop:	lls	72		clear buffer
	eax7	bufl
	staq	buf-2,7
	eax7	-2,7
	tpl	*-2

	tsx2	space		blank buffer
	tsx2	print

	eax0	20
reread:	tsx2	xio_wait		start tape io
	vfd	18/buf,12/bufl
	zero	tpch,5
	tra	tper

	stc1	ptbfirst		kill dup line suppress
	eax1	0		print each 64 words so we can 
l1:	eax7	buf,1
	stx7	pbuf		fill in addr
	eaa	0,1
	tsx2	octwd
	arl	12
	ora	=h    00
	sta	line+1
	tsx2	ptblock
	zero	pbuf,64
	eax1	64,1
	cmpx1	bufl,du
	tmi	l1

	tra	loop

tper:	arl	30
	ana	=o7,dl		dispatch on major status
	tra	*+1,al
	tra	retry
	tra	reread
	tra	reread
	tra	retry
	tra	eof
	tra	reread
	tra	*
	tra	*

retry:	eax0	-1,0		count down errors
	tmi	rder
	tsx2	xio_wait		backspace
	zero
	zero	tpch,38
	tra	reread
	tra	reread

rder:	tsx2	space		here on tape error
	tsx2	print
	lda	=herror "
	tra	*+4

eof:	tsx2	space
	tsx2	print
	lda	=heof   "
	sta	line
	tsx2	print
	tra	loop

ttyrequest:
	tsx2	wteof		eject paper
x2:	eax2	*
	lca	=1,dl
	tra	mem|1,2

"
nheading:	bci	'1     tape dump   '
	equ	nheading_lth,*-nheading

	even
buf:	bss	,((bufl+63)/64)*64

	even
pbuf:	arg	0
tpch:	vfd	18/0,6/1,12/0


	include	pt_equ
	include	bos_tv
	include	bos_common

	end
"
"
"                                          -----------------------------------------------------------
"
"
"
" Historical Background
"
" This edition of the Multics software materials and documentation is provided and donated
" to Massachusetts Institute of Technology by Group Bull including Bull HN Information Systems Inc. 
" as a contribution to computer science knowledge.  
" This donation is made also to give evidence of the common contributions of Massachusetts Institute of Technology,
" Bell Laboratories, General Electric, Honeywell Information Systems Inc., Honeywell Bull Inc., Groupe Bull
" and Bull HN Information Systems Inc. to the development of this operating system. 
" Multics development was initiated by Massachusetts Institute of Technology Project MAC (1963-1970),
" renamed the MIT Laboratory for Computer Science and Artificial Intelligence in the mid 1970s, under the leadership
" of Professor Fernando Jose Corbato. Users consider that Multics provided the best software architecture for
" managing computer hardware properly and for executing programs. Many subsequent operating systems
" incorporated Multics principles.
" Multics was distributed in 1975 to 2000 by Group Bull in Europe , and in the U.S. by Bull HN Information Systems Inc., 
" as successor in interest by change in name only to Honeywell Bull Inc. and Honeywell Information Systems Inc. .
"
"                                          -----------------------------------------------------------
"
" Permission to use, copy, modify, and distribute these programs and their documentation for any purpose and without
" fee is hereby granted,provided that the below copyright notice and historical background appear in all copies
" and that both the copyright notice and historical background and this permission notice appear in supporting
" documentation, and that the names of MIT, HIS, Bull or Bull HN not be used in advertising or publicity pertaining
" to distribution of the programs without specific prior written permission.
"    Copyright 1972 by Massachusetts Institute of Technology and Honeywell Information Systems Inc.
"    Copyright 2006 by Bull HN Information Systems Inc.
"    Copyright 2006 by Bull SAS
"    All Rights Reserved
"
"