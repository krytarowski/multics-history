" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1982 *
" *                                                         *
" ***********************************************************
" 
"
" Modified 83-10-10 by E. N. Kittlitz for disks up to 63.

	name	rdlabl

	include	bosequ
" 
rdlabl:	stx2	rdl_x2
	tsx2	init_io
	tsx2	ttyinit
	arg	ttyrequest

	lda	geastab		set for MVT instruction
	sta	gvmvt		..

	ldx2	rdl_x2		get command line and scan it ourselvs

	lda	mem|0,2		get address of command line
	ana	=-1,dl		..
	als	2		get character offset
	mlr	(pr,al),()	copy command line
	desc9a	mem|0,84
	desc9a	line,84

	tsx2	scan		now scan to get drive number

	lda	arg_count,*	lets check to see
	cmpa	2,du		if there are enough args.....
	tnz	rdl_fix_args	tell him how to use the command

	mvt	(id),(),fill(20)
	arg	earg+1		change subsystem BCD format
	desc9a	subsysname,4	to ascii format
gvmvt:	arg	*

	lda	arg+2		drive number
	cmpa	63,dl		don't allow numbers > idcw device code
	tpnz	rdl_bad_drive	tell him of the problem...

	als	18		set up the drive number 
	sta	devid		for find disk
	lda	subsysname
	tsx2	find_disk		check to see if the subsystem
	arg	devid		is where we think....
	tra	rdl_subsys_err

	tsx2	query_disk	read the storage system label
	arg	devid
	tra	rdl_disk_error
	tra	rdl_not_vol
	tra	rdl_return

rdl_fix_args:
	tsx2	erpt		tell him how to use the command
	acc	"Wrong number of arguments supplied should be: "
	tra	rdl_usage

rdl_bad_drive:
	tsx2	erpt		
	acc	"drive number is greater than 63"
	tra	rdl_usage

rdl_subsys_err:
	tsx2	erpt		find_disk error return 
	acc	"Bad disk subsystem supplied"
	tra	rdl_usage

rdl_disk_error:
	tsx2	erpt		query_disk error return
	acc	"error detected in reading disk"
	tra	rdl_return
rdl_not_vol:
	tsx2	erpt		query_disk return
	acc	"not a multics storage system volume"
	tra	rdl_return

rdl_usage:
	tsx2	erpt
	acc	"   rdlabl dskn yy"

ttyrequest:
rdl_return:
rdl_x2:
	eax2	*		return pointer
	lca	1,dl		don't chain to another command
	tra	mem|1,2		head for setup.........

devid:	bss	,1
subsysname:
	bss	,2

" \014
	include	qdisk
" 
	include	config_cards
" 
	include	fs_vol_label
" 
"	include	old_fs_vol_label
" 
	include	bos_sdw
" 
	include	bos_tv
" 
	include	bos_common
	end	
"
"
"                                          -----------------------------------------------------------
"
"
"
" Historical Background
"
" This edition of the Multics software materials and documentation is provided and donated
" to Massachusetts Institute of Technology by Group Bull including Bull HN Information Systems Inc. 
" as a contribution to computer science knowledge.  
" This donation is made also to give evidence of the common contributions of Massachusetts Institute of Technology,
" Bell Laboratories, General Electric, Honeywell Information Systems Inc., Honeywell Bull Inc., Groupe Bull
" and Bull HN Information Systems Inc. to the development of this operating system. 
" Multics development was initiated by Massachusetts Institute of Technology Project MAC (1963-1970),
" renamed the MIT Laboratory for Computer Science and Artificial Intelligence in the mid 1970s, under the leadership
" of Professor Fernando Jose Corbato. Users consider that Multics provided the best software architecture for
" managing computer hardware properly and for executing programs. Many subsequent operating systems
" incorporated Multics principles.
" Multics was distributed in 1975 to 2000 by Group Bull in Europe , and in the U.S. by Bull HN Information Systems Inc., 
" as successor in interest by change in name only to Honeywell Bull Inc. and Honeywell Information Systems Inc. .
"
"                                          -----------------------------------------------------------
"
" Permission to use, copy, modify, and distribute these programs and their documentation for any purpose and without
" fee is hereby granted,provided that the below copyright notice and historical background appear in all copies
" and that both the copyright notice and historical background and this permission notice appear in supporting
" documentation, and that the names of MIT, HIS, Bull or Bull HN not be used in advertising or publicity pertaining
" to distribution of the programs without specific prior written permission.
"    Copyright 1972 by Massachusetts Institute of Technology and Honeywell Information Systems Inc.
"    Copyright 2006 by Bull HN Information Systems Inc.
"    Copyright 2006 by Bull SAS
"    All Rights Reserved
"
"