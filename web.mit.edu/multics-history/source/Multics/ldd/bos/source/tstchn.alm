" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1982 *
" *                                                         *
" ***********************************************************
" A program to allow exploring the IOM from command level in BOS.
" Written in August 1971 by R F Mabee.
" Modified 3/22/74 by Noel I. Morris


"  ******************************************************
"  *                                                    *
"  *                                                    *
"  * Copyright (c) 1972 by Massachusetts Institute of   *
"  * Technology and Honeywell Information Systems, Inc. *
"  *                                                    *
"  *                                                    *
"  ******************************************************



" 

" Usage:		TSTCHN  channo  -device-  -type-  -waittime-

" device, type, and waittime are optional. channo specifies the IOM channel to use.
" device selects a unit for tape or disk controllers.
" type is either PSIA or CPCHAN.  The default is PSIA.
" waittime indicates how long to wait for status. The default is about three seconds.

" The command reads lines from the console consisting of a sequence of
" requests. All of the requests on the line are used to form a DCW list
" which is then started. All of the fields shown must be specified.
" Any status is reported online.
" Allowable requests:
"	Q		to return to command level. Hitting
"			request key also works.
"	N <op> <rep>	issues non-data transfer command <op>
"			with repetition count of <rep>. A count
"			of zero implies 64.
"	W <op> <tal>	unit record data transfer <op> with data
"			DCW tally of <tal>, from 2048-word
"			buffer.
"	R <op> <tal>	same as W, except that first four words
"			of buffer are typed after operation.
"	M		turns on marker bit in previous
"			instruction DCW.
"	C		turns on character tally bit in previous
"			data DCW.
"	D <i> <word>	sets i'th word of the buffer to <word>.
"			Has no effect on DCW list.
"	P <i>		peeks at the i'th word of the buffer.

" Example:
" TSTCHN 12 1	* Using tape (channel 12) drive 1.
" N 70 1	* Rewind.
" STATUS: 400002 000000 000000 000000
" R 05 420 M R 05 420  * Read a 420-word binary record, give marker interrupt, read another record.
" STATUS: 400020 000000 012773 040000
" STATUS: 400000 000000 012773 040000
" DATA READ: ....
" Q
" BOS AT ....

" 

	name	tstchn


	include	bosequ

" 

tstchn:	stx2	x2_save
	ldx3	mem|0,2
	lda	mem|1,3
	tmi	Q
	als	18
	sta	channo
	als	9
	sta	tstpcw+1

	lda	mem|2,3
	tpl	*+2
	lda	0,dl
	als	24
	sta	device
	stca	tstpcw,20

	lda	mem|3,3
	tmi	*+3
	era	=h  psia
	sta	typesw

	lda	mem|4,3
	tmi	*+2
	sta	wait_time

	tsx2	init_io
	tsx2	ttyinit
	arg	ttyrequest

cycle:	tsx2	nextline
	tsx2	scan

	stz	rswitch
	eax3	0
	eax1	0
	eax4	0

next_arg:	lda	arg,4
	eax7	0
	rpt	ntable/2,2,tze
	cmpa	table,7
	ttf	-1,7

	tsx2	erpt
	acc	"Unknown request."
	tra	cycle

table:	bci	'     W'
	tra	W
	bci	'     R'
	tra	R
	bci	'     N'
	tra	N
	bci	'     M'
	tra	M
	bci	'     C'
	tra	C
	bci	'     D'
	tra	D
	bci	'     P'
	tra	P
	bci	'     Q'
	tra	Q
	oct	777777777777
	tra	run

	equ	ntable,*-table

" 

Q:
ttyrequest:
x2_save:	eax2	*
	lca	1,dl
	tra	mem|1,2

R:	stc1	rswitch

W:	lda	arg+1,4
	als	30
	ora	device
	ora	=o720000,dl
	sta	dcwlist,1
	absa	buffer
	als	6
	sta	dcwlist+1,1

	lda	arg+2,4
	ana	=o7777,dl
	orsa	dcwlist+1,1

	eax3	0,1
	eax1	2,1
	eax4	3,4
	tra	next_arg

N:	lda	arg+1,4
	als	30
	ora	device
	ora	=o720200,dl
	sta	dcwlist,1
	lda	arg+2,4
	ana	=o77,dl
	orsa	dcwlist,1

	eax3	0,1
	eax1	1,1
	eax4	3,4
	tra	next_arg

M:	cmpx1	0,du
	tze	*+3
	lda	=o010000,dl
	orsa	dcwlist,3

	eax4	1,4
	tra	next_arg

C:	cmpx1	0,du
	tze	*+3
	lda	=o040000,dl
	orsa	dcwlist-1,1
	eax4	1,4
	tra	next_arg

D:	lda	arg+1,4
	ldq	arg+2,4
	stq	buffer,al
	eax4	3,4
	tra	next_arg

P:	lda	arg+1,4
	ldq	buffer,al
	stq	temp
	eax0	0,4		save X4
	tsx2	erpt
	acc	"^w"
	arg	temp
	eax4	2,0
	tra	next_arg

run:	cmpx1	0,du
	tze	cycle
	lda	=o777700717777
	ansa	dcwlist,3

	szn	typesw
	tze	connect_psia

	lda	dcwlist
	sta	tstpcw
	tsx2	connect_iom
	zero	tstpcw,dcwlist+1
	tra	wait

connect_psia:
	tsx2	connect_iom
	zero	tstpcw,dcwlist

wait:	lxl3	wait_time
waitloop:	sbx3	1,du
	tze	io_done
	tsx2	check_status
	arg	channo
	tra	waitloop
	nop			" Treat bad status same as good.

	staq	statusword
	tsx2	erpt
	acc	"status: ^w ^w."
	arg	statusword
	arg	statusword+1

	tra	waitloop-1

io_done:	szn	rswitch
	tze	cycle

	tsx2	erpt
	acc	"data read: ^w ^w ^w ^w."
	arg	buffer
	arg	buffer+1
	arg	buffer+2
	arg	buffer+3

	tra	cycle

" 

device:	zero
channo:	zero
rswitch:	zero
temp:	zero
typesw:	bss	,1
wait_time:
	dec	5000
	even
tstpcw:	vfd	o6/40,6/0,6/0,3/7,3/2,6/2,6/1,9/0,27/
statusword:
	bss	,2
dcwlist:	bss	,20
buffer:	bss	,2048

	include	bos_tv
	include	bos_common

	end
"
"
"                                          -----------------------------------------------------------
"
"
"
" Historical Background
"
" This edition of the Multics software materials and documentation is provided and donated
" to Massachusetts Institute of Technology by Group Bull including Bull HN Information Systems Inc. 
" as a contribution to computer science knowledge.  
" This donation is made also to give evidence of the common contributions of Massachusetts Institute of Technology,
" Bell Laboratories, General Electric, Honeywell Information Systems Inc., Honeywell Bull Inc., Groupe Bull
" and Bull HN Information Systems Inc. to the development of this operating system. 
" Multics development was initiated by Massachusetts Institute of Technology Project MAC (1963-1970),
" renamed the MIT Laboratory for Computer Science and Artificial Intelligence in the mid 1970s, under the leadership
" of Professor Fernando Jose Corbato. Users consider that Multics provided the best software architecture for
" managing computer hardware properly and for executing programs. Many subsequent operating systems
" incorporated Multics principles.
" Multics was distributed in 1975 to 2000 by Group Bull in Europe , and in the U.S. by Bull HN Information Systems Inc., 
" as successor in interest by change in name only to Honeywell Bull Inc. and Honeywell Information Systems Inc. .
"
"                                          -----------------------------------------------------------
"
" Permission to use, copy, modify, and distribute these programs and their documentation for any purpose and without
" fee is hereby granted,provided that the below copyright notice and historical background appear in all copies
" and that both the copyright notice and historical background and this permission notice appear in supporting
" documentation, and that the names of MIT, HIS, Bull or Bull HN not be used in advertising or publicity pertaining
" to distribution of the programs without specific prior written permission.
"    Copyright 1972 by Massachusetts Institute of Technology and Honeywell Information Systems Inc.
"    Copyright 2006 by Bull HN Information Systems Inc.
"    Copyright 2006 by Bull SAS
"    All Rights Reserved
"
"