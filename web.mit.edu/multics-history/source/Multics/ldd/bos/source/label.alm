" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1982 *
" *                                                         *
" ***********************************************************
"  ******************************************************
"  *                                                    *
"  *                                                    *
"  * Copyright (c) 1972 by Massachusetts Institute of   *
"  * Technology and Honeywell Information Systems, Inc. *
"  *                                                    *
"  *                                                    *
"  ******************************************************

"
"	label
"
"	Program to read the label of a tape.
"	Coded 2/72 Steve Webber
"	Modified 9/83 Sherman D. Sprague to be more verbose.
"
	include	bosequ
	include	mstr

	equ	bufl,1040		max record size

	stx2	x2
	ldx7	mem|0,2
	lda	mem|1,7		get tape number
	tmoz	*+5
	cmpa	=8,du
	tpl	*+3
	als	12
	stca	tpch,04		store as device number

	ldx7	com|tapechanno	copy tape channel number into control word
	stx7	tpch

	tsx2	init_io
	tsx2	ttyinit
	arg	return

"	Rewind the tape

	tsx2	xio_wait
	zero	0,0
	zero	tpch,56
	tra	*-3

	tsx2	itaper
	arg	tpch

	eax0	20
reread:	tsx2	xio_wait		start tape io
	vfd	18/buf,12/bufl
	zero	tpch,5
	tra	tper

" 
"	Have a look at the label

	lda	buf+mstr.head+mstr_header.c1
	cmpa	=v36/header_c1	" Ordinary tape?
	tnz 	not_old_label


" Old tape, no understanding 'cept "offset 16"

	tsx2	erpt
	acc	"Old format Multics label ^A."
	arg	old_label_desc

	lda	buf+mstr_header.uid
	ldq	buf+mstr_header.uid+1
	tra	date_time_common

not_old_label:

	lda	buf+mst_label.head+mstr_header.c1
	cmpa	=v36/header_c1
	tze	new_label

	tsx2	erpt
	acc	"Not a valid Multics tape."
	tra	rewind_the_tape

old_label_desc:
	desc9a	buf+16,32
vid.tape_reel_id:
	desc9a	buf+mst_label.vid+volume_identifier.tape_reel_id,32
vid.installation_id:
	desc9a	buf+mst_label.vid,32	" name is too long
vid.volume_set_id:
	desc9a	buf+mst_label.vid+volume_identifier.volume_set_id,32
new_label:
	tsx2	erpt
	acc	"Multics tape label ^A."	
	arg	vid.tape_reel_id
	tsx2	erpt
	acc	"   Volume set:     ^A."
	arg	vid.volume_set_id
	tsx2	erpt
	acc	"   Created at:     ^A"
	arg	vid.installation_id
	lda	buf+mst_label.head+mstr_header.uid
	ldq	buf+mst_label.head+mstr_header.uid+1

date_time_common:
	cana	=o000001,du	" unique_bits_ ?
	tze	already_date_time
	lls	18
	lrl	20

already_date_time:

	tsx2	cvclock
	staq	date_time_string
	tsx2	erpt
	acc	"   Created on:     ^g ^g."
	arg	date_time_string
	arg	date_time_string+1

rewind_the_tape:		

"	Now rewind the tape

	tsx2	xio_wait
	zero	0,0
	zero	tpch,56
	tra	*+1

return:
x2:
	eax2	*
	lca	1,dl
	tra	mem|1,2

tper:	arl	30
	ana	=o7,dl		dispatch on major status
	tra	*+1,al
	tra	retry
	tra	reread
	tra	reread
	tra	retry
	tra	eof
	tra	reread
	tra	*
	tra	*

retry:	eax0	-1,0		count down errors
	tmi	rder
	tsx2	xio_wait		backspace
	zero
	zero	tpch,38
	tra	reread
	tra	reread

rder:	tsx2	erpt
	acc	"tape error"
	tra	return

eof:	tsx2	erpt
	acc	"eof, no label"
	tra	return

	even
	bss	buf,bufl

	even
tpch:	vfd	18/0,6/1,12/0
	even
	bss	date_time_string,2
	

	include	bos_tv
	include	bos_common

	end
"
"
"                                          -----------------------------------------------------------
"
"
"
" Historical Background
"
" This edition of the Multics software materials and documentation is provided and donated
" to Massachusetts Institute of Technology by Group Bull including Bull HN Information Systems Inc. 
" as a contribution to computer science knowledge.  
" This donation is made also to give evidence of the common contributions of Massachusetts Institute of Technology,
" Bell Laboratories, General Electric, Honeywell Information Systems Inc., Honeywell Bull Inc., Groupe Bull
" and Bull HN Information Systems Inc. to the development of this operating system. 
" Multics development was initiated by Massachusetts Institute of Technology Project MAC (1963-1970),
" renamed the MIT Laboratory for Computer Science and Artificial Intelligence in the mid 1970s, under the leadership
" of Professor Fernando Jose Corbato. Users consider that Multics provided the best software architecture for
" managing computer hardware properly and for executing programs. Many subsequent operating systems
" incorporated Multics principles.
" Multics was distributed in 1975 to 2000 by Group Bull in Europe , and in the U.S. by Bull HN Information Systems Inc., 
" as successor in interest by change in name only to Honeywell Bull Inc. and Honeywell Information Systems Inc. .
"
"                                          -----------------------------------------------------------
"
" Permission to use, copy, modify, and distribute these programs and their documentation for any purpose and without
" fee is hereby granted,provided that the below copyright notice and historical background appear in all copies
" and that both the copyright notice and historical background and this permission notice appear in supporting
" documentation, and that the names of MIT, HIS, Bull or Bull HN not be used in advertising or publicity pertaining
" to distribution of the programs without specific prior written permission.
"    Copyright 1972 by Massachusetts Institute of Technology and Honeywell Information Systems Inc.
"    Copyright 2006 by Bull HN Information Systems Inc.
"    Copyright 2006 by Bull SAS
"    All Rights Reserved
"
"