" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1982 *
" *                                                         *
" * Copyright (c) 1972 by Massachusetts Institute of        *
" * Technology and Honeywell Information Systems, Inc.      *
" *                                                         *
" ***********************************************************

" The WRITE command for BOS.
" This command causes the rest of the command line to be typed online,
" regardless of the state of the quiet switch.  If the first argument
" is ALERT, then WRITE does not type anything.  Instead, the "audible alarm"
" is turned on to get the operator's attention.

" Created 4 August 1971 by R F Mabee.
" Modified 3/5/74 by N. I. Morris for SCC operation.
" Last modified 10/75 by Noel I. Morris

	name	write


	include	bosequ


" 

write:	stx2	x2_save
	tsx2	init_io		" Initialize for I/O.

	lda	com|ttychanno	" set tty channel #
	sta	ttyno
	als	9
	sta	wpcw+1

	tsx2	ttyinit		" initialize for special interrupt
	arg	ttyrequest

	ldx2	x2_save
	ldx7	mem|0,2		" Relative pointer to argument list.
	lda	mem|1,7		" First arg, 6 GEBCD characters.
	cmpa	=h alert		" ALERT?
	tze	ring_alarm

	lxl6	mem|0,2		" Get location of command line.
	stx6	lineloc		" Save for address computation.

	ldq	mem|2*(arglen+1),7	" Get descriptor for zeroth arg.
	anq	=o077777,du	" Remove segment tag field.
	sblq	lineloc		" Get offset from beginning of line.
	qrl	18		" In QL.
	mpy	6,dl		" Compute character offset.
	adq	6,dl		" But skip "WRITE".
	stq	off		" And save.

	lda	mem|2*(arglen+1),7	" Get argument descriptor again.
	adla	1,du		" Bump past "WRITE".
	sta	desc		" Save descriptor.
	arl	15		" Character position in AL.
	ana	=o7,dl		" Mask character position.
	adla	off		" Add to total offset.
	neg	0		" Negate.
	adla	84,dl		" Compute characters remaining on line.
	stca	desc,03		" Set character length in descriptor.

	mlr	(pr,id),(),fill(20)	" Copy the rest of line.
	arg	desc
	desc6a	line,84

	tsx2	type		" Write it out.
	zero	line,14

ttyrequest:
out:
x2_save:	eax2	*
	lca	1,dl		" Indicate no chaining.
	tra	mem|1,2		" Back to setup.

" 

ring_alarm:
	tsx2	connect_iom	" Connect the IOM.
	zero	wpcw,0

	tsx2	check_status	" Wait for completion.
	arg	ttyno
	tra	*-2
	tra	*+1		" Ignore errors.
	tra	out		" Done.

" 

desc:	bss	,1		" Descriptor for copying line.
off:	bss	,1		" Used in offset computation.
lineloc:	bss	,1		" Location of command line.


ttyno:	vfd	18/0

	even

wpcw:	vfd	o6/51,6/0,6/0,o3/7,3/0,6/2,6/1	" Write alert command.
	vfd	o9/0

	include	bos_tv
	include	bos_common

	end
"
"
"                                          -----------------------------------------------------------
"
"
"
" Historical Background
"
" This edition of the Multics software materials and documentation is provided and donated
" to Massachusetts Institute of Technology by Group Bull including Bull HN Information Systems Inc. 
" as a contribution to computer science knowledge.  
" This donation is made also to give evidence of the common contributions of Massachusetts Institute of Technology,
" Bell Laboratories, General Electric, Honeywell Information Systems Inc., Honeywell Bull Inc., Groupe Bull
" and Bull HN Information Systems Inc. to the development of this operating system. 
" Multics development was initiated by Massachusetts Institute of Technology Project MAC (1963-1970),
" renamed the MIT Laboratory for Computer Science and Artificial Intelligence in the mid 1970s, under the leadership
" of Professor Fernando Jose Corbato. Users consider that Multics provided the best software architecture for
" managing computer hardware properly and for executing programs. Many subsequent operating systems
" incorporated Multics principles.
" Multics was distributed in 1975 to 2000 by Group Bull in Europe , and in the U.S. by Bull HN Information Systems Inc., 
" as successor in interest by change in name only to Honeywell Bull Inc. and Honeywell Information Systems Inc. .
"
"                                          -----------------------------------------------------------
"
" Permission to use, copy, modify, and distribute these programs and their documentation for any purpose and without
" fee is hereby granted,provided that the below copyright notice and historical background appear in all copies
" and that both the copyright notice and historical background and this permission notice appear in supporting
" documentation, and that the names of MIT, HIS, Bull or Bull HN not be used in advertising or publicity pertaining
" to distribution of the programs without specific prior written permission.
"    Copyright 1972 by Massachusetts Institute of Technology and Honeywell Information Systems Inc.
"    Copyright 2006 by Bull HN Information Systems Inc.
"    Copyright 2006 by Bull SAS
"    All Rights Reserved
"
"