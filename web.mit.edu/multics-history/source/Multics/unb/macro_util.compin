.* ***********************************************************
.* *                                                         *
.* *                                                         *
.* * Copyright, (C) Honeywell Information Systems Inc., 1981 *
.* *                                                         *
.* *                                                         *
.* ***********************************************************
.*				MACRO UTILITY
.*				ENTRY VECTOR
.ur .go %InputFileName%
.*				OUTPUT
.la output
.srv FONT "l%LEVEL%font"
.ts "%ENTRY%" = "exact"
.srv FONT "l4exact"
.ur .ur .tlh 0 0 |%%.fnt %FONT%%%%%Parameter%%%%.fnt%%|
.rt
.*				BOX SETUP
.la setbox
.ts %TITLEN% = 0			;.* is there a title?
.rt
.*
.srv BOX_LENGTH 0
.srv BOXI 1
.la BOX_LOOP
.ts %BOXI% = 1 & %FORM% < 5
.ur .srv BOX_WORD1 "%%[before [ltrim [after "%TITLE1%" ":"]] ","]%%" 
.ts %BOXI% > 1 | %FORM% = 5
.ur .srv BOX_WORD%BOXI% "%%TITLE%BOXI%%%"
.ur .ur .ur .sr BOX_LENGTH %%%%%%[max %BOX_LENGTH% %%%%{Measure("%%BOX_WORD%BOXI%%%")}%%%%]%%%%%%
.srv BOXI %BOXI% + 1
.ts %BOXI% <= %TITLEN%
.go BOX_LOOP
.sr BOX_LINE "_____________________________________________X"(1,%BOX_LENGTH%)
.rt
.*				PROSE BOX OFF
.la prose_box_off			;.* obsolete entry point
.ifi set_page_header
.rt
.*				TOC ENTRY TITLE SPLITTER
.la split_title
.srv TOC_REF "  %PageNo%"
.ts "%section%" = ""
.go NO_SECTION
.*
.sr LSN Measure("%section%")
.sr TOC_REF "   "(1,3-%LSN%)
.sr TOC_REF "%TOC_REF%%PageNo%"
.*
.la NO_SECTION
.ur .srv TITLE1 "%%[translate "%Parameter%" " " "!"]%%"
.srv TITLEN 1
.*
.la PART_LOOP
.ur .ts Measure("%%TITLE%TITLEN%%%") <= %LEVEL_SIZE%
.rt
.*
.*.ur .ur .ur .ur .srv PART_LENGTH %LEVEL_SIZE% - %%%%%%%%%%[index "%%%%%%[reverse "%%%%[substr "%%TITLE%TITLEN%%%" 1 %LEVEL_SIZE%]%%%%"]%%%%%%" " "]%%%%%%%%%%
.ur .ur .srv PART_LENGTH Wordl("%%TITLE%TITLEN%%%", %LEVEL_SIZE%)
.ur .ur .srv TITLE%%{%TITLEN% + 1}%% "%%TITLE%TITLEN%%%"(%PART_LENGTH% + 1)
.ur .srv TITLE%TITLEN% "%%TITLE%TITLEN%%%"(1,%PART_LENGTH%)
.srv TITLEN %TITLEN% + 1
.go PART_LOOP
.*				TITLE BREAKER
.la break_title
.srv TITLE "%Parameter%"		;.* copy input arg
.srv TITLE1 "%Parameter%"		;.* in case of no breaker
.srv TITLEN 1			;.* title line counter
.srv SCNDX 1			;.* line scanner
.*
.la BREAKER_LOOP
.ur .ur .srv BRKR %%%%[index "%%{"%TITLE%"(%SCNDX%)}%%" "!"]%%%% ;.* find one
.ts %BRKR% = 0			;.* no more, all done
.rt
.*				is it protected?
.srv BRK_PROT "%TITLE%"(%SCNDX% + %BRKR% - 3,3)
.ts "%BRK_PROT%" ^= "**!" & "%BRK_PROT%" ^= "¿*!" & "%BRK_PROT%"(2) = "*!"
.go NO_BREAK			;.* yes
.*
.ur .srv TITLE%TITLEN% "%TITLE%"(%SCNDX%,%BRKR% - 1)
.srv TITLEN %TITLEN% + 1
.ur .srv TITLE%TITLEN%  "%TITLE%"(%SCNDX% + %BRKR%)
.srv TITLE "%TITLE%"(1,%SCNDX% + %BRKR% - 2)" ""%TITLE%"(%SCNDX% + %BRKR%)
.*
.la NO_BREAK
.srv SCNDX %SCNDX% + %BRKR%
.go BREAKER_LOOP

.la onecol
.ts "%DeviceClass%" ^= "bitmap"	;.* bitmap only
.rt
.srv LEFT_GUTTER 5
.pd  66,70,0,5,60		;.* sets 36pi on 42pi page w/ 3pi gutters both sides
.srv COLWIDTH 60
.rt

.la twocol
.srv LEFT_GUTTER 0
.ts "%DeviceClass%" ^= "bitmap"
.go NOT_PHOTO
.pd 66,71,33,4,33		;.* sets 2 x 19.8pi cols with 2.4pi gutter
.sr COLWIDTH 33
.rt

.la NOT_PHOTO
.ur .srv GUTTER %%{3 + %MPM_SPACE%}%%
.ur .srv COLWIDTH %%{(%PageWidth% - %GUTTER%) / 2}%%
.ur .pdc %COLWIDTH%,%GUTTER%,%COLWIDTH%
.rt

.la midbox
.la pagebox
.la prose_box			;.* obsolete entry point
.brb				;.* clean up
.srv FORM 5			;.* this is an exact form
.srv FONT "l4exact"			;.* in roman font
.srv FONT_SIZE 12
.ur .ifi break_title "%Parameter%"
.*
.la mhbox
.la hbox
.ifi set_page_header		;.* clear page header
.ts "%Parameter%" = ""		;.* null title
.rt
.* set box stuff
.ifi setbox
.*
.ts "%InputFileName%" ^= "mhbox" & "%InputFileName%" ^= "midbox"
.go HBOX
.ur .ur .spt %%{2 + %MPM_SPACE%}%%	;.* put in needed space
.ur .ur .brn %%{%TITLEN% + 3 * %MPM_SPACE% + %Widow% + 6}%%
.*
.ts (%PageLine% = 0) & (%Galley% = 0)	;.* no mid-page box needed
.go HBOX
.srv PageBlock -1			;.* this is a page block
.tbb h				;.* build the midpage box
.fif
.inl 0
.ur .fnt l4font %FONT_SIZE%
.ur |%BOX_LINE%||%BOX_LINE%|
.spf
.srv BOXI 1
.la BOX_LOOP_MHBOX
.ur .ur |%PAD%%%BOX_WORD%BOXI%%%||%PAD%%%BOX_WORD%BOXI%%%|
.srv BOXI %BOXI% + 1
.ts %BOXI% <= %TITLEN%
.go BOX_LOOP_MHBOX
.ur |%BOX_LINE%||%BOX_LINE%|
.ur .ur .spf %%{%LABELSPACE% + %MPM_SPACE%}%%
.fnt
.tbe
.srv PageBlock 0
.srv MHBOX -1			;.* set flag showing a midpage box
.*
.la HBOX
.hbb a				;.* build the page head box
.fif
.inl 0
.ur .fnt %FONT% %FONT_SIZE%
.ur |%BOX_LINE%||%BOX_LINE%|
.spf
.srv BOXI 1
.la BOX_LOOP_HBOX
.ur .ur |%PAD%%%BOX_WORD%BOXI%%%||%PAD%%%BOX_WORD%BOXI%%%|
.srv BOXI %BOXI% + 1
.ts %BOXI% <= %TITLEN%
.go BOX_LOOP_HBOX
.ur |%BOX_LINE%||%BOX_LINE%|
.fnt
.hbe
.rt
.*				 build page header
.la set_page_header
.hla
.ts %PAGE_HEADERN% = 0
.rt
.*
.ts %ParamPresent%
.ur .ifi break_title "%Parameter%"
.*.fnt l0font
.srv I 1
.la PAGE_HEADER_LOOP
.ur .ur .hla %I% ||%PAD%%%PAGE_HEADER%I%%%|
.srv I %I% + 1
.ts %I% <= %PAGE_HEADERN%
.go PAGE_HEADER_LOOP
.*.fnt
.rt


                                          -----------------------------------------------------------


Historical Background

This edition of the Multics software materials and documentation is provided and donated
to Massachusetts Institute of Technology by Group BULL including BULL HN Information Systems Inc. 
as a contribution to computer science knowledge.  
This donation is made also to give evidence of the common contributions of Massachusetts Institute of Technology,
Bell Laboratories, General Electric, Honeywell Information Systems Inc., Honeywell BULL Inc., Groupe BULL
and BULL HN Information Systems Inc. to the development of this operating system. 
Multics development was initiated by Massachusetts Institute of Technology Project MAC (1963-1970),
renamed the MIT Laboratory for Computer Science and Artificial Intelligence in the mid 1970s, under the leadership
of Professor Fernando Jose Corbato. Users consider that Multics provided the best software architecture 
for managing computer hardware properly and for executing programs. Many subsequent operating systems 
incorporated Multics principles.
Multics was distributed in 1975 to 2000 by Group Bull in Europe , and in the U.S. by Bull HN Information Systems Inc., 
as successor in interest by change in name only to Honeywell Bull Inc. and Honeywell Information Systems Inc. .

                                          -----------------------------------------------------------

Permission to use, copy, modify, and distribute these programs and their documentation for any purpose and without
fee is hereby granted,provided that the below copyright notice and historical background appear in all copies
and that both the copyright notice and historical background and this permission notice appear in supporting
documentation, and that the names of MIT, HIS, BULL or BULL HN not be used in advertising or publicity pertaining
to distribution of the programs without specific prior written permission.
    Copyright 1972 by Massachusetts Institute of Technology and Honeywell Information Systems Inc.
    Copyright 2006 by BULL HN Information Systems Inc.
    Copyright 2006 by Bull SAS
    All Rights Reserved
